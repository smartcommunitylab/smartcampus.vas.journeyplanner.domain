/*******************************************************************************
 * Copyright 2012-2013 Trento RISE
 * 
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 * 
 *        http://www.apache.org/licenses/LICENSE-2.0
 * 
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 ******************************************************************************/
package smartcampus.services.journeyplanner;

import it.sayservice.platform.compiler.domain.model.annotations.ServiceNotification;
import it.sayservice.platform.compiler.domain.model.annotations.Service;
import it.sayservice.platform.compiler.domain.model.annotations.Subscription;
import it.sayservice.platform.compiler.domain.model.annotations.Local;
import it.sayservice.platform.compiler.domain.model.annotations.Process;

import it.sayservice.platform.smartplanner.data.message.Itinerary;
import it.sayservice.platform.smartplanner.data.message.Position;
import it.sayservice.platform.smartplanner.data.message.journey.RecurrentJourney;
import it.sayservice.platform.smartplanner.data.message.journey.JourneyPlannerUserProfile;

import it.sayservice.platform.smartplanner.data.message.alerts.AlertAccident;
import it.sayservice.platform.smartplanner.data.message.alerts.AlertDelay;
import it.sayservice.platform.smartplanner.data.message.alerts.AlertParking;
import it.sayservice.platform.smartplanner.data.message.alerts.AlertStrike;
import it.sayservice.platform.smartplanner.data.message.alerts.AlertRoad;

import smartcampus.services.journeyplanner.helpers.AlertFilter;
import smartcampus.services.journeyplanner.helpers.DelayChecker;
import smartcampus.services.journeyplanner.helpers.ParkingChecker;
import smartcampus.services.journeyplanner.helpers.AlertUpdater;
import smartcampus.services.journeyplanner.helpers.AlertsSent;

import smartcampus.services.oraritreni.delays.GenericTrain;

import smartcampus.service.trentomale.data.message.Trentomale;
import smartcampus.services.trentomale.TrainsAlertsSent;

import eu.trentorise.smartcampus.service.parcheggi.data.message.Parcheggi.Parcheggio;

import eu.trentorise.smartcampus.services.ordinanzerovereto.data.message.Ordinanzerovereto.Ordinanza;

import eu.trentorise.smartcampus.service.tobike.data.message.Tobike.Stazione;

import smartcampus.services.parkings.Parking;
import smartcampus.services.parkings.ParkingsAlertsSent;


DomainType UserAlertSender {
    port alertDelay(alert: AlertDelay, userId : String, clientId : String, title: String);
    port alertStrike(alert: AlertStrike, userId : String, clientId : String, title: String);  
	port alertParking(alert: AlertParking, userId : String, clientId : String, title: String);
	port alertAccident(alert: AlertAccident, userId : String, clientId : String, title: String);  
	port alertRoad(alert: AlertRoad, userId : String, clientId : String, title: String);  
}

// SINGLE

DomainObject ItineraryFactory {

    @Local(actionName="saveItinerary")
    operation saveItinerary(itinerary:Itinerary,clientId:String,userId:String,originalFrom:Position,originalTo:Position,name:String);
    
   action saveItinerary(itinerary:Itinerary,clientId:String,userId:String,originalFrom:Position,originalTo:Position,name:String) {
		create ItineraryObject(data:itinerary,clientId:clientId,userId:userId,originalFrom:originalFrom,originalTo:originalTo,name:name);
	}
	
}

DomainType ItineraryObject implements UserAlertSender {

	public immutable var userId : String;
	public immutable var clientId : String;
	public var data: Itinerary;
	public immutable var originalFrom: Position;
	public immutable var originalTo: Position;
	public immutable var name : String;
	
	public var monitor: boolean = true; 

    @Local(actionName="deleteItinerary")
    operation deleteItinerary();
    
    @Local(actionName="setMonitorFlag")
    operation setMonitorFlag(flag:boolean) returns boolean;    
    
    action deleteItinerary() => terminate;
    
    action setMonitorFlag(flag:boolean) returns boolean {
    	set monitor = flag;
    	return flag;
    }
    
    action checkDelay(alert:AlertDelay) {
		if (AlertFilter.filterDelay(data, alert)) {
			set data = AlertUpdater.updateAlerts(data,alert);
			publish alertDelay(alert, userId, clientId, name);
		}
    }
    
   action checkStrike(alert:AlertStrike) {
		if (AlertFilter.filterStrike(data, alert)) {
			set data = AlertUpdater.updateAlerts(data,alert);
			publish alertStrike(alert, userId, clientId, name);
		} 
    }    
    
   action checkParking(alert:AlertParking) {
        newAlert: AlertParking = AlertFilter.filterParking(data, alert);
		if (newAlert != null) {
			set data = AlertUpdater.updateAlerts(data,newAlert);
			publish alertParking(newAlert, userId, clientId, name);		
		} 
    }      

   action checkAccident(alert:AlertAccident) {
		if (AlertFilter.filterAccident(data, alert)) {
			set data = AlertUpdater.updateAlerts(data,alert);
			publish alertAccident(alert, userId, clientId, name);		
		} 
    }      
    
    action checkRoad(alert:AlertRoad) {
		if (AlertFilter.filterRoad(data, alert)) {
			set data = AlertUpdater.updateAlerts(data,alert);
			publish alertRoad(alert, userId, clientId, name);		
		} 
    }      
    
	subscribe AlertFactory!alertAccident() if (monitor) => checkAccident(alert);
	subscribe AlertFactory!alertRoad() if (monitor) => checkRoad(alert);
	subscribe AlertFactory!alertDelay() if (monitor) => checkDelay(alert);
	subscribe AlertFactory!alertStrike() if (monitor) => checkStrike(alert);
	subscribe AlertFactory!alertParking() if (monitor) => checkParking(alert);
	
	subscribe TrainsAlertsSender!alertDelay() if (monitor) => checkDelay(alert);
	subscribe ParkingAlertsSender!alertParking() if (monitor) => checkParking(alert);
    
}

// RECURRENT

DomainObject RecurrentJourneyFactory {

    @Local(actionName="saveRecurrentJourney")
    operation saveRecurrentJourney(recurrentJourney:RecurrentJourney,clientId:String,userId:String,name:String,monitor:boolean);
    
   action saveRecurrentJourney(recurrentJourney:RecurrentJourney,clientId:String,userId:String,name:String,monitor:boolean) {
		create RecurrentJourneyObject(data:recurrentJourney,clientId:clientId,userId:userId,name:name,monitor:monitor);
	}
}	

DomainType RecurrentJourneyObject implements UserAlertSender{

	public immutable var userId : String;
	public immutable var clientId : String;
	public var name : String;
	public var data: RecurrentJourney; 
	
	public var monitor: boolean; 
	
	private var alerts : AlertsSent;
	
	@Local(actionName="updateRecurrentJourney")
    operation updateRecurrentJourney(newJourney:RecurrentJourney,newName:String,newMonitor:boolean);	
	
    @Local(actionName="deleteRecurrentJourney")
    operation deleteRecurrentJourney();
    
    @Local(actionName="setMonitorFlag")
    operation setMonitorFlag(flag:boolean) returns boolean;    

    action updateRecurrentJourney(newJourney:RecurrentJourney, newName:String, newMonitor:boolean) {
    	set data = newJourney;
    	set name = newName;
    	set monitor = newMonitor;
    }
    
    action deleteRecurrentJourney() => terminate;
    
    action setMonitorFlag(flag:boolean) returns boolean {
    	set monitor = flag;
    	return flag;
    }
    
    action checkDelay(alert:AlertDelay) {
		if (AlertFilter.filterDelay(data, alert, alerts)) { 
			publish alertDelay(alert, userId, clientId, name);
			set alerts = AlertUpdater.updateAlerts(alert, alerts);
		}
    }
    
   action checkStrike(alert:AlertStrike) {
		if (AlertFilter.filterStrike(data, alert, alerts)) {
			publish alertStrike(alert, userId, clientId, name);
			set alerts = AlertUpdater.updateAlerts(alert, alerts);
		} 
    }    
    
   action checkParking(alert:AlertParking) {
		if (AlertFilter.filterParking(data, alert, alerts)) {
			publish alertParking(alert, userId, clientId, name);		
			set alerts = AlertUpdater.updateAlerts(alert, alerts);
		} 
    }      
    
   action checkAccident(alert:AlertAccident) {
		if (AlertFilter.filterAccident(data, alert, alerts)) {
			publish alertAccident(alert, userId, clientId, name);		
			set alerts = AlertUpdater.updateAlerts(alert, alerts);
		} 
    }      
    
    action checkRoad(alert:AlertRoad) {
		if (AlertFilter.filterRoad(data, alert, alerts)) {
			publish alertRoad(alert, userId, clientId, name);		
			set alerts = AlertUpdater.updateAlerts(alert, alerts);
		} 
    }      
    
	subscribe AlertFactory!alertAccident() if (monitor) => checkAccident(alert);
	subscribe AlertFactory!alertRoad() if (monitor) => checkRoad(alert);
	subscribe AlertFactory!alertDelay() if (monitor) => checkDelay(alert);
	subscribe AlertFactory!alertStrike() if (monitor) => checkStrike(alert);
	subscribe AlertFactory!alertParking() if (monitor) => checkParking(alert);
    
    subscribe TrainsAlertsSender!alertDelay() if (monitor) => checkDelay(alert);
	subscribe ParkingAlertsSender!alertParking() if (monitor) => checkParking(alert);    
    
    }

// ALERTS

DomainObject AlertFactory {

    port alertDelay(alert: AlertDelay);
    port alertStrike(alert: AlertStrike);    
	port alertParking(alert: AlertParking);
	port alertAccident(alert: AlertAccident);
	port alertRoad(alert: AlertRoad);   	

    @Local(actionName="submitAlertAccident")
    operation submitAlertAccident(newAlert:AlertAccident);    
    
    @Local(actionName="submitAlertDelay")
    operation submitAlertDelay(newAlert:AlertDelay);
    
    @Local(actionName="submitAlertParking")
    operation submitAlertParking(newAlert:AlertParking); 
    
    @Local(actionName="submitAlertStrike")
    operation submitAlertStrike(newAlert:AlertStrike);     

    @Local(actionName="submitAlertRoad")
    operation submitAlertRoad(newAlert:AlertRoad);    

	action submitAlertAccident(newAlert:AlertAccident) {
		publish alertAccident(newAlert);
	}	

	action submitAlertDelay(newAlert:AlertDelay) {
		publish alertDelay(newAlert);
	}
	
	action submitAlertParking(newAlert:AlertParking) {
		publish alertParking(newAlert);
	}		
	
	action submitAlertStrike(newAlert:AlertStrike) {
		publish alertStrike(newAlert);
	}			

	action submitAlertRoad(newAlert:AlertRoad) {
		publish alertRoad(newAlert);
	}			

}


// SERVICES

DomainObject GetTrains {


    @ServiceNotification(serviceId="smartcampus.service.TrentoMale", methodName="TrainsTrentoMale", converter="smartcampus.services.trentomale.TrentoMaleConverter")
    port updateTrentoMale(data : GenericTrain[]);

    @ServiceNotification(serviceId="smartcampus.service.oraritreni", methodName="GetOrariTreni", converter="smartcampus.services.oraritreni.OrariTreniConverter")
    port updateOrariTreni(data : GenericTrain[]);  

    @Subscription(serviceId="smartcampus.service.TrentoMale", methodName="TrainsTrentoMale")
    operation subscribeTrentoMale();
    
    @Subscription(serviceId="smartcampus.service.oraritreni", methodName="GetOrariTreni")
    operation subscribeTrenitalia(stazione:String);    
    
    action initialize() {
        invoke subscribeTrentoMale();    
        invoke subscribeTrenitalia("trento");
        invoke subscribeTrenitalia("bassano del grappa");
	}

}

DomainObject TrainsAlertsSender {

    port alertDelay(alert: AlertDelay);
    
    public var sent : TrainsAlertsSent;

	action sendAlerts(list : GenericTrain[]) {
	        for (t in list) {
	        	delay : AlertDelay = DelayChecker.checkDelay(t);
	        	if (delay != null) {
	        		newSent : TrainsAlertsSent = DelayChecker.checkNewAlerts(sent,t);
	        		if (newSent != null) {
	        			set sent = newSent;
						publish alertDelay(delay);
					}
	        	}
	        }
	       set sent = DelayChecker.cleanOldAlerts(sent);
	}
	
	action initialize() {
		set sent = TrainsAlertsSent.getInstance();
	}

   subscribe GetTrains!updateTrentoMale() => sendAlerts(data);
   subscribe GetTrains!updateOrariTreni() => sendAlerts(data);
}



DomainObject GetParking {


    @ServiceNotification(serviceId="smartcampus.service.parcheggi", methodName="GetParcheggiTrento", converter="smartcampus.services.parkings.ParcheggiTrentoConverter")
    port updateParcheggiTrento(data : Parking[]);
    
    @ServiceNotification(serviceId="smartcampus.service.parcheggi", methodName="GetParcheggiRovereto", converter="smartcampus.services.parkings.ParcheggiRoveretoConverter")
    port updateParcheggiRovereto(data : Parking[]);    

    @Subscription(serviceId="smartcampus.service.parcheggi", methodName="GetParcheggiTrento")
    operation subscribeParcheggiTrento();
    
    @Subscription(serviceId="smartcampus.service.parcheggi", methodName="GetParcheggiRovereto")
    operation subscribeParcheggiRovereto();    
    
    action initialize() {
        invoke subscribeParcheggiTrento();
        invoke subscribeParcheggiRovereto();        
	}
}

DomainObject GetStazioni {

    @ServiceNotification(serviceId="smartcampus.service.tobike", methodName="GetStazioni", converter="smartcampus.services.parkings.StazioniTobikeConverter")
    port updateStazioni(data : Parking[]);

    @Subscription(serviceId="smartcampus.service.tobike", methodName="GetStazioni")
    operation subscribeStazioni(user:String,password:String,code:String);
    
    action initialize() {
               invoke subscribeStazioni("rovereto", "Rov14!", "5061");  
	}

}




DomainObject ParkingAlertsSender {

    port alertParking(alert: AlertParking);
    port alertAllParking(alert: AlertParking);
    
    public var sent : ParkingsAlertsSent;

	action sendAlerts(list : Parking[]) {
	        for (t in list) {
	        	parking : AlertParking = ParkingChecker.checkParking(t);
	        	publish alertAllParking(parking);
	        	if (parking != null) {
	        		newSent : ParkingsAlertsSent = ParkingChecker.checkNewAlerts(sent,t);
	        		if (newSent != null) {
	        			set sent = newSent;
						publish alertParking(parking);
					}
	        	}
	        }
	       set sent = ParkingChecker.cleanOldAlerts(sent);
	}
	
	action initialize() {
		set sent = ParkingsAlertsSent.getInstance();
	}

   subscribe GetParking!updateParcheggiTrento() => sendAlerts(data);
   subscribe GetParking!updateParcheggiRovereto() => sendAlerts(data);
   subscribe GetStazioni!updateStazioni() => sendAlerts(data);
}

DomainType RoadAlertSender {
	port sendRoadAlerts(data: AlertRoad[]);
}

DomainObject GetOrdinanzeRovereto implements RoadAlertSender {

    @ServiceNotification(serviceId="eu.trentorise.smartcampus.services.ordinanzerovereto.OrdinanzeRoveretoService", methodName="GetOrdinanze", converter="smartcampus.services.ordinanze.OrdinanzeRoveretoConverter")
	port sendRoadAlerts(data: AlertRoad[]);

    @Subscription(serviceId="eu.trentorise.smartcampus.services.ordinanzerovereto.OrdinanzeRoveretoService", methodName="GetOrdinanze")
    operation subscribeOrdinanzeRovereto();
    
    action initialize() {
        invoke subscribeOrdinanzeRovereto();    
	}
}



